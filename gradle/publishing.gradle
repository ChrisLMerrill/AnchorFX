apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'net.nemerosa.versioning'

def pomConfig = {
    name project.name
    description 'AnchorFX'
    url 'https://github.com/alexbodogit/AnchorFX'
    inceptionYear '2015'
    licenses {
        license([:]) {
            name 'LGPL-3.0'
            url 'https://opensource.org/licenses/LGPL-3.0'
            distribution 'repo'
        }
    }
    scm {
        url 'https://github.com/alexbodogit/AnchorFX.git'
    }
    developers {
        [
            alexbodogit: 'Alessio Vinerbi'
        ].each { devId, devName ->
            developer {
                id devId
                name devName
                roles {
                    role 'Developer'
                }
            }
        }
    }
}

publishing {
    publications {
        mavenCustom(MavenPublication) {
            from components.java
            artifact sourceJar

            pom.withXml {
                // all dependencies should use the default scope (compile) but
                // Gradle insists in using runtime as default
                asNode().dependencies.dependency.each { dep ->
                    if (dep.scope.text() == 'runtime') {
                        dep.remove(dep.scope)
                    }
                }
                asNode().children().last() + pomConfig
            }
        }
    }
}

jar {
    manifest {
        attributes(
            'Built-By': System.properties['user.name'],
            'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString(),
            'Build-Date': project.buildDate,
            'Build-Time': project.buildTime,
            'Build-Revision': versioning.info.commit,
            'Specification-Title': project.name,
            'Specification-Version': project.version,
            'Implementation-Title': project.name,
            'Implementation-Version': project.version
        )
    }
    metaInf {
        from rootProject.file('LICENSE')
    }
}

if (!project.hasProperty('bintrayUsername')) ext.bintrayUsername = ''
if (!project.hasProperty('bintrayApiKey')) ext.bintrayApiKey = ''

bintray {
    user = project.bintrayUsername
    key = project.bintrayApiKey
    publications = ['mavenCustom']
    pkg {
        repo = 'maven'
        userOrg = 'alexbodogit'
        name = project.name
        desc = project.name
        licenses = ['LGPL-3.0']
        labels = ['javafx', 'docking']
        websiteUrl = 'https://github.com/alexbodogit/AnchorFX'
        issueTrackerUrl = 'https://github.com/alexbodogit/AnchorFX/issues'
        vcsUrl = 'https://github.com/alexbodogit/AnchorFX.git'
        publicDownloadNumbers = true
    }
}